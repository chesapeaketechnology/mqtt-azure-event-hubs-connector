buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:6.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

mainClassName = 'com.chesapeaketechnology.mqtt.connector.MqttAzureEventHubsConnectorMain'
group 'com.chesapeaketechnology'
version '0.1.5-SNAPSHOT'

sourceCompatibility = 1.11

wrapper {
    gradleVersion = '6.3'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    slf4jVersion = '1.7.30'
}

configurations {
    includeInJar
}

configurations.compile.extendsFrom(configurations.includeInJar)

dependencies {
    includeInJar 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    includeInJar 'com.azure:azure-messaging-eventhubs:5.2.0'

    includeInJar 'com.typesafe:config:1.4.0'
    includeInJar 'com.orbitz.consul:consul-client:1.4.0'

    includeInJar "org.slf4j:slf4j-api:${slf4jVersion}"
    includeInJar "org.slf4j:slf4j-log4j12:${slf4jVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from {
        configurations.includeInJar.collect { it.isDirectory() ? it : zipTree(it) }
    }

    /*into('lib') {
        from configurations.includeInJar
    }*/

    manifest.attributes(
            'Main-Class': 'com.chesapeaketechnology.mqtt.connector.MqttAzureEventHubsConnectorMain',
            'Class-Path': configurations.runtimeClasspath.files.collect { 'lib/' + it.getName() }.join(' '),
    )

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

docker {
    javaApplication {
        baseImage = 'openjdk:11'
        maintainer = 'Christian Rowlands'
        images = ["chesapeaketechnology/${rootProject.name}:${version}", "chesapeaketechnology/${rootProject.name}:latest"]
    }
}

dockerCreateDockerfile {
    instruction 'RUN mkdir -p /mqtt-azure-connector/config /mqtt-azure-connector/log'
    instruction 'VOLUME ["/mqtt-azure-connector/config", "/mqtt-azure-connector/log"]'
    entryPoint 'java', '-cp', '/mqtt-azure-connector/config:/app/resources:/app/classes:/app/libs/*', 'com.chesapeaketechnology.mqtt.connector.MqttAzureEventHubsConnectorMain'
}