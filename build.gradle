buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:6.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

mainClassName = 'com.chesapeaketechnology.mqtt.connector.MqttAzureEventHubsConnectorMain'
group 'com.chesapeaketechnology'
version '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '5.4.1'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    slf4jVersion = '1.7.25'
}

configurations {
    includeInJar
}

configurations.compile.extendsFrom(configurations.includeInJar)

dependencies {
    includeInJar 'com.azure:azure-messaging-eventhubs:5.0.1'

    includeInJar 'com.typesafe:config:1.4.0'

    includeInJar "org.slf4j:slf4j-api:${slf4jVersion}"
    includeInJar "org.slf4j:slf4j-log4j12:${slf4jVersion}"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Make sure all jars in the includeInJar configuration are included in the lib directory in the jar that is assembled.
jar {
    from {
        configurations.includeInJar.collect { it.isDirectory() ? it : zipTree(it) }
    }

    /*into('lib') {
        from configurations.includeInJar
    }*/

    manifest.attributes(
            'Main-Class': 'com.chesapeaketechnology.mqtt.connector.MqttAzureEventHubsConnectorMain',
            'Class-Path': configurations.runtimeClasspath.files.collect { 'lib/' + it.getName() }.join(' '),
    )

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

docker {
    javaApplication {
        baseImage = 'openjdk:8'
        maintainer = 'Christian Rowlands'
    }
}